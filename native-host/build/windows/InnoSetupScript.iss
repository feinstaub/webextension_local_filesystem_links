; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)

; signool not properly setup yet - key missing --> skipped signing for now and check it later. First fix path issue after installation.
; SignTool=signtool
#define AppVersion GetFileVersion("D:\github\webextension_local_filesystem_links\native-host\bin\win32\local-link-messaging-host\local-link-messaging-host.exe")
#define AppName "Local file links Native Messaging API Host"

AppId={{318A38DD-A431-42F0-BBAB-58C330AC545F}
AppName={#AppName} v{#AppVersion}
AppVerName={#AppName} {#AppVersion}
VersionInfoVersion={#AppVersion}
AppVersion={#AppVersion}
;AppVerName=Local file links Native Messaging API Host
AppPublisher=feinstaub/webextension_local_filesystem_links
AppPublisherURL=https://github.com/feinstaub/webextension_local_filesystem_links
AppSupportURL=https://github.com/feinstaub/webextension_local_filesystem_links
AppUpdatesURL=https://github.com/feinstaub/webextension_local_filesystem_links
DefaultDirName={%USERPROFILE}\Local_file_links-Native_API_Host
DisableDirPage=yes
DefaultGroupName=Local file links Native Messaging API Host
DisableProgramGroupPage=yes
OutputDir=D:\github\webextension_local_filesystem_links\native-host\bin\win32
OutputBaseFilename=native-app-setup
Compression=lzma
UninstallDisplayIcon=D:\github\webextension_local_filesystem_links\native-host\src\addon_icon_48.ico
SetupIconFile=D:\github\webextension_local_filesystem_links\native-host\src\addon_icon_48.ico
SolidCompression=yes
PrivilegesRequired=none

[Tasks]
Name: Visualc; Description: Install Visual C++ re-distributable (required for the app - install if not already available); Flags: unchecked

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "D:\github\webextension_local_filesystem_links\native-host\bin\win32\local-link-messaging-host\*"; DestDir: "{app}"
Source: "D:\github\webextension_local_filesystem_links\native-host\src\webextension_local_filesystem_links_win.json"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\github\webextension_local_filesystem_links\native-host\build\windows\vcredist_x86.exe"; DestDir: "{app}"; AfterInstall: RunOtherInstaller; Tasks: Visualc; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
; Name: "{group}\Local file links Native Messaging API Host"; Filename: "{app}\local-link-messaging-host.exe"

[Registry]
Root: HKCU; Subkey: "Software\Mozilla\NativeMessagingHosts\webextension_local_filesystem_links"; ValueType: string; ValueName: ""; ValueData: "{app}\webextension_local_filesystem_links_win.json"; Flags: uninsdeletekey

[Code]
procedure RunOtherInstaller;
var
  ResultCode: Integer;
begin
  if not ShellExec('runas', ExpandConstant('{app}\vcredist_x86.exe'), '', '', SW_SHOW,
          ewWaitUntilTerminated, ResultCode)
  then
    MsgBox('Other installer failed to run!' + #13#10 +
      SysErrorMessage(ResultCode), mbError, MB_OK);
end;
